Program.Sub.ScreenSU.Start
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start

Program.Sub.Preflight.End

Program.Sub.Main.Start
F.Intrinsic.Control.SetErrorHandler("Main_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sSQL.Declare(String)
V.Local.sRouter.Declare(String)
'Hook 11210

F.Intrinsic.UI.InvokeWaitDialog("Taking router snapshot. Please wait","Please wait....")

V.Local.sRouter.Set(V.Passed.008000)
F.ODBC.Connection!con.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)
Function.Intrinsic.String.Build("Delete from GCG_3882_Router_Snapshot where Terminal='{0}' and Router='{1}'",V.Caller.Terminal,V.Local.sRouter.trim,V.Local.sSQL)
F.ODBC.Connection!con.Execute(V.Local.sSQL)

F.ODBC.Connection!con.OpenRecordsetRW("rst","Select * from GCG_3882_Router_Snapshot")

Function.Intrinsic.String.Build("Select Line_Router, LMO, Run_Time, Set_Up, Rate, Operation, Part_WC_Outside from Router_Line where Router='{0}' and Router_Type Like 'L' and LMO<>'C' order by Line_Router",V.Local.sRouter.trim,V.Local.sSQL)
F.ODBC.Connection!con.OpenRecordsetRO("rstR",V.Local.sSQL)
F.Intrinsic.Control.dountil(V.ODBC.con!rstR.EOF,=,True)
	F.ODBC.con!rst.AddNew
	F.ODBC.con!rst.Set!Terminal(V.Caller.Terminal)
	F.ODBC.con!rst.Set!Router(V.Local.sRouter)
	F.ODBC.con!rst.Set!Seq(V.ODBC.con!rstR.FieldValTrim!Line_Router)
	F.ODBC.con!rst.Set!LMO(V.ODBC.con!rstR.FieldValTrim!LMO)

	F.Intrinsic.Control.If(V.ODBC.con!rstR.FieldValTrim!LMO,=,"M")
		F.ODBC.con!rst.Set!Qty(V.ODBC.con!rstR.FieldValLong!Run_Time)
		F.ODBC.con!rst.Set!Runtime(0)
		F.ODBC.con!rst.Set!Setup(V.ODBC.con!rstR.FieldValTrim!Set_Up)
		F.ODBC.con!rst.Set!Part(V.ODBC.con!rstR.FieldValTrim!Part_WC_Outside)
		F.ODBC.con!rst.Set!Rate(V.ODBC.con!rstR.FieldValLong!Rate)

	F.Intrinsic.Control.Elseif(V.ODBC.con!rstR.FieldValTrim!LMO,=,"L")
		F.ODBC.con!rst.Set!Runtime(V.ODBC.con!rstR.FieldValLong!Run_Time)
		F.ODBC.con!rst.Set!Setup(V.ODBC.con!rstR.FieldValTrim!Set_Up)
		F.ODBC.con!rst.Set!Qty(0)
		F.ODBC.con!rst.Set!Rate(V.ODBC.con!rstR.FieldValLong!Rate)

	F.Intrinsic.Control.ElseIf(V.ODBC.con!rstR.FieldValTrim!LMO,=,"O")
		F.ODBC.con!rst.Set!Runtime(V.ODBC.con!rstR.FieldValLong!Run_Time)
		F.ODBC.con!rst.Set!Setup(V.ODBC.con!rstR.FieldValTrim!Set_Up)
		F.ODBC.con!rst.Set!Qty(0)
		F.ODBC.con!rst.Set!Vendor(V.ODBC.con!rstR.FieldValTrim!Operation)
		F.ODBC.con!rst.Set!Rate(V.ODBC.con!rstR.FieldValLong!Rate)

	F.Intrinsic.Control.endif
	
	F.ODBC.con!rst.Update

	F.ODBC.con!rstR.MoveNext
F.Intrinsic.Control.Loop
F.ODBC.con!rstR.Close
F.ODBC.con!rst.close

F.ODBC.Connection!con.Close

F.Intrinsic.UI.CloseWaitDialog

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Main_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_3882_Router_Snapshot_Cert.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End
Function.Intrinsic.Control.EndIf


Program.Sub.Main.End

Program.Sub.Comments.Start
${$0$}$GCG_Router_Snapshot_Cert$}$Strider$}$7/29/2015
${$1$}$$}$$}$0$}$11210$}$RES010A-POPULATE-HOOK$}$10/6/2015 3:13:00 PM$}$(Program: RE0072; Screen: RES010A)

${$2$}$$}$$}$1$}$4$}$GCG_ROUTER_SNAPSHOT$}$10/6/2015 3:17:30 PM$}$SQL Table used
${$2$}$$}$$}$0$}$2$}$GCG_ROUTER_CHANGE_CERT.gas$}$10/6/2015 3:13:00 PM$}$
${$3$}$0$}$$}$0$}$-1$}$$}$12:00:00 AM$}$This is used to create a before snapshot of the router information for GCG_Router_Change_Cert.gas
Program.Sub.Comments.End

